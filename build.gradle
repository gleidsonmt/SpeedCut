plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
}

group 'io.github.gleidsonmt.speedcut'
version '1.0'
mainClassName = 'io.github.gleidsonmt.speedcut.Start'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.1'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'io.github.gleidsonmt.speedcut'
    mainClass = mainClassName
}

javafx {
    version = '17.0.1'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
}

dependencies {

    implementation 'org.yaml:snakeyaml:1.29'

    implementation files('vendor/GNDecorator-1.0.3.jar')
    implementation files('vendor/scenicView.jar')

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'SpeedCut'
    }

    jpackage {
        imageOptions = [
                '--icon', 'logo.ico'
        ]
        installerOptions = [
            '--description', "Project Example for JavaFx",
            '--copyright', 'Copyrigth Â© 2021-2022 GLEIDSON NEVES DA SILVEIRA',
            '--icon', 'logo.ico'
        ]

        addExtraDependencies("javafx")
    }
}

jlinkZip {
    group = 'distribution'
}

jar {

    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }

}